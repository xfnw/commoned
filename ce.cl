; -*- lisp -*-

(require :asdf)
; pregexp seems to not have a provide, so we cannot require
; instead try loading it if not already bundled in via asdf
(when (not (boundp '*pregexp-version*))
 (load "pregexp/pregexp.lisp"))

(defvar ce-commands-alist '((#\q . quit)
 (#\Newline . ce-command-enter)
 (#\: . ce-command-eval)
 (#\; . ce-command-eval-region)
 (#\{ . ce-command-expand-before)
 (#\} . ce-command-expand)
 (#\= . ce-command-get-point)
 (#\, . ce-command-swap-point)
 (#\/ . ce-command-search)
 (#\? . ce-command-search-before)
 (#\a . ce-command-add)
 (#\A . ce-command-add-before)
 (#\c . ce-command-line-replace)
 (#\d . ce-command-delete)
 (#\e . ce-command-open)
 (#\h . ce-command-help)
 (#\i . ce-command-insert)
 (#\I . ce-command-insert-beg)
 (#\m . ce-command-move)
 (#\p . ce-command-print)
 (#\s . ce-command-reg-replace)
 (#\t . ce-command-copy)
 (#\w . ce-command-write)
 (#\0 . ce-command-number)
 (#\1 . ce-command-number)
 (#\2 . ce-command-number)
 (#\3 . ce-command-number)
 (#\4 . ce-command-number)
 (#\5 . ce-command-number)
 (#\6 . ce-command-number)
 (#\7 . ce-command-number)
 (#\8 . ce-command-number)
 (#\9 . ce-command-number)
 ))

(defvar buffer nil)
(defvar filename nil)
(defvar query nil)
(defvar ins nil)
(defvar sfl nil)
(defvar err nil)

; newpoint values:
; 0 - reusing previous point
; 1 - outpoint set, inpoint is outpoint
; 2 - inpoint set, outpoint is eof
; 3 - both set
(defvar newpoint 0)
(defvar inpoint 0)
(defvar outpoint -1)
(defvar inline-inpoint 0)
(defvar inline-outpoint -1)

(defun concat (&rest args)
  "concatenate strings together but with format"
  (format nil "狺狎珞┅ㄤ彐躅铒舡铛憝铄鳝ㄣⅡ弭躜铋殒铛礤蜷矧铄黛轭澧铒矧ㄤ殓轸汨狎悌ㄣ栳蚪＼五黛轭悌┅ㄤ彐躅沐盹铛溟雯㈨镤蹯躞怩栳钿戾弪铫ㄩ溟雯盹铛溟雯┅ㄤ彐躅沐瘐箬扉铄ㄩ钿屮扉铄瘐箬扉铄轭麸翳怩骀弪狒轭溴ㄩ轭溴轭溴轶箴邈獒狍怩骀弪轶箝铉禊扉铍邃扉篝箦赳怩骀弪ㄣ镱扉铄怩骀弪┅瘐箬扉铄ㄣ潋铘桡潋ū轭溴怩骀弪┅┅ㄤ彐躅沐瘐箬扉铄ㄩ钿屮扉铄螬瘐箬扉铄轭麸翳怩骀弪狒轭溴ㄩ扉铄痱镧ㄣ瀛瘐箬扉铄轭溴ㄣ狎扉铄螬ㄣ瀛瘐箬扉铄ū轭溴ㄣ潋扉铄螬┅┅ㄤ彐躅沐溴戾翦ㄩ秕舂溴戾翦蜥铉镦扉铄螈箦赳怩骀弪钽镱篚怏羼怩骀弪轭铘桡潋ū秕舂怩骀弪┅┅ㄤ彐躅沐麸脲铙篝麸镳糸镱犰戾戾铉翳篝颟篝癌ㄣ躜癌ㄢ铋飑秕铋飑蝈痱彗屮痱彗屮瓠聃雉ㄦ矧磲铋④荥恽麸氅┅ㄩ铙ㄦ矧磲铋恽麸氅┅Ⅳ镫孱辁镦篝狒汨狎徙翦麸氘蝈箴邈趔忉汶箪狍桢骘弩汜痖铉ㄩ沲戾瞟戾èㄣ栳篝沲颟┅ㄩ怏灬篝汨狎麽忉汶箪狍璎殓铒蝈翳轶镱ㄣ瀛麸脲铙篝麸戾篝ū沲颟铋秕蝈轭螬ㄩㄣ栳蚪麸悌ㄣ瀛麸脲铙篝麸戾ū沲颟ū沲颟铋ㄣ镱痱彗屮瓠蝈痨徙濯蝈篚怏羼篝篝沲颟轭螬秕舂蝈轭螬ㄣ瀛麸脲铙篝麸戾篝ū沲颟ㄣ栳蚪＼悌秕蝈轭螬┅蝈鲥蝮ㄩ篝沲颟秕翳弪麽趄衢扉铉溴扉黹翦颥殓铒蝈轸ㄣ镱痱彗屮瓠蝈痨徙濯蝈篚怏羼篝篝沲颟轭螬秕舂┅┅韵南痫篌殁禊骒狒翦翳蝈玳镱桢蝈轭篝遽镦铄狎禊弼弪泔眄犷铄邃轭沐盹麸珏痱镳弪铛礅弪罂泔眄犷潴翳狒汨犷珏铛礅弪镦扉铄黠蹯汨犷珏翳蝈玳镱犷麽ㄤ彐躅沐蝈箦舡轭瘐ī㈡轼痫轭麸犰祜轭瘐趑轭铄铛礅弪蟋箬秕熹忮汜祆邃狒翳忮玳铑轭镦盹篝泔眄犷潴ㄩ铄黟镩铘箦赳轭痫轭秕麴镩铘┅箦赳铄黟镩铘癌ㄤ彐躅沐蝈痨ī疳蝮泔眄犷潴骝镯篝溟睥祜镳戾è轭瘐蝈徜汨狎┅戾è沩ㄣ潋ㄡ篌镢轭瘐沐泔眄犷潴犰轶舂┅ㄩ沩ㄨ犷潇弪汜箦ㄦ躅汜祆沩轭瘐舂ㄥ蝌矧ㄥ箦赳弪濠ㄦ矧磲⒖ア┅痱镧蝈徜扉铄箦赳铄黟镩铘癌ㄦ矧磲⒖ア┅┅┅ㄤ彐躅沐磲轭ī㈤铋翎扉泔眄镱邃骝镯忾睥戾è狎珞ㄣ潋ㄥ艉泔眄犷洵狎珞┅┅ㄣ狍扉篝戾铉翳狎珞òīūㄣ瀛镳孱ㄣ狎狎珞┅雉桢蝼轶ㄦ矧磲⒖ア┅┅ㄣ瀛蝈痨ㄥ艉聃轸癌ㄤ彐躅沐泔眄犷洵孱翦é镳糸镱犰悌痱镢弩铄黛轭弩殒铒遽翦怡犷雉桢泔眄犷洧ㄤ邈灬蝈ㄩ珙矧悌ㄩ铄黟镩铘ㄩ窘ū秕麴镩铘扉篝戾铉翳怩骀弪┅痱镧ㄦ矧磲⒖ア蝈趱蝾骝镯沐泔眄犷洵孱翦颟痱镧箦赳秕麴镩铘ū秕麴镩铘┅箦赳轭痫轭秕麴镩铘┅ㄣ瀛蝈箦舡轭瘐舂戾è秕ㄣ瀛盹秕麴镩铘扉篝戾铉翳怩骀弪┅┅ㄦ矧磲狺アㄣ狎铘桡潋秕怩骀弪┅┅ㄤ彐躅沐泔眄犷洵弼犰é镳糸镱犰悌㈠鲠祯狒扉箴屮痱弩箝镱ㄤ邈灬蝈ㄩ珙矧悌ㄣ瀛蝈箦舡轭瘐舂ㄦ矧磲狺アㄥ鲠蝈徜┅┅ㄤ彐躅沐泔眄犷洵弼犰蝈玳镱é镳糸镱犰悌㈠鲠祯狒骈蝮屮痱弩箝镱轭蝈玳镱ㄤ邈灬蝈ㄩ珙矧悌ㄣ瀛蝈箦舡轭瘐舂蝈徜扉铄戾è盱孱扉篝戾铉翳怩骀弪┅戾è轭ㄣ瀛盹轭痫轭盱孱┅秕ūㄣ瀛盹秕麴镩铘盱孱┅┅ㄦ矧磲狺アㄥ鲠蝈徜骝镯篝蜷铉ㄦ矧磲铋狺" (subseq buffer in out))))))))

(defun ce-walk-match (dir match n stop &optional (offset 0))
  "increment n in dir direction until line matches match
  or n reaches stop"
  (if (or (= n stop) (= (+ n offset) stop))
   (progn (format t "?%") stop)
   (let ((nn (+ n dir)))
    ; using nth like this is a bit silly and inefficent
    ; when walking forwards, but we can then reuse the
    ; function when walking backwards
    (if (pregexp-match-positions match (nth (+ nn offset) buffer))
     nn
     (ce-walk-match dir match nn stop offset)))))

(defun ce-command-expand-before (&optional c)
  "decrement inpoint until line matches argument"
  (declare (ignore c))
  (ce-reset-input)
  (let ((inp (read-line)) (len (list-length buffer)))
   ; TODO: checking the same condition twice is silly,
   ; find a better way
   (let ((off (if (string= "" inp) -1 0))
	 (match (pregexp (if (string= "" inp) "^[[:space:]]*$" inp))))
    (setq
     inpoint
     (ce-walk-match -1 match (ce-mod inpoint len) 0 off)))))

(defun ce-command-expand (&optional c)
  "increment outpoint until line matches argument"
  (declare (ignore c))
  (ce-reset-input)
  (let ((inp (read-line)) (len (list-length buffer)))
   (let ((match (pregexp (if (string= "" inp) "^[[:space:]]*$" inp))))
    (setq
     outpoint
     (ce-walk-match 1 match (ce-mod outpoint len) (1- len))))))

(defun ce-command-get-point (&optional c)
  "print the point"
  (declare (ignore c))
  (ce-reset-input)
  (read-line)
  (let ((len (list-length buffer)))
   (if (not (= inpoint outpoint))
    (format t "a," (ce-mod inpoint len)))
   (format t "a a%" (ce-mod outpoint len) filename)))

(defun ce-command-swap-point (&optional c)
  "set the inpoint to recent outpoint or beginning, outpoint to eof

  for example:
  3,5 selects lines 3 through 5 (inclusive)
  ,5  selects from the beginning of the document through line 5
  4,  selects from line 4 to the end of the document"
  (declare (ignore c))
  (if (= 0 newpoint)
   (setq inpoint 0)
   (setq inpoint outpoint))
  (setq outpoint -1)
  (setq newpoint 2))

(defun ce-command-search (&optional c)
  "set point at next line to match argument"
  (declare (ignore c))
  (ce-reset-input)
  (let ((inp (read-line)) (len (list-length buffer)))
   (let ((match
	  (pregexp (if (string= "" inp) query (setq query inp)))))
    (setq
     outpoint
     (ce-walk-match 1 match (ce-mod inpoint len) (1- len)))))
  (setq inpoint outpoint)
  (format t "a%" (car (nthcdr outpoint buffer))))

(defun ce-command-search-before (&optional c)
  "set point at previous line to match argument"
  (declare (ignore c))
  (ce-reset-input)
  (let ((inp (read-line)) (len (list-length buffer)))
   (let ((match
	  (pregexp (if (string= "" inp) query (setq query inp)))))
    (setq
     inpoint
     (ce-walk-match -1 match (ce-mod outpoint len) 0))))
  (setq outpoint inpoint)
  (format t "a%" (car (nthcdr outpoint buffer))))

(defun ce-add-til-dot (index lines)
  "read input until dot, add to buffer"
  (let ((line (read-line)))
   (if (string= "." line)
    (progn
     (setq inpoint index)
     (setq outpoint (+ index (1- (list-length lines))))
     (ce-push-lines index (reverse lines)))
    (ce-add-til-dot index (cons line lines)))))

(defun ce-common-add (index)
  "common parts of ce-command-add and ce-command-add-before"
  (let ((line (read-line)))
   (if (string= "" line)
    (ce-add-til-dot index nil)
    (progn
     (setq inpoint index)
     (setq outpoint index)
     (ce-push-line index line)))))

(defun ce-command-add (&optional c)
  "add lines after point"
  (declare (ignore c))
  (ce-reset-input)
  (if buffer
   (ce-common-add (1+ (ce-mod outpoint (list-length buffer))))
   (ce-common-add 0)))

(defun ce-command-add-before (&optional c)
  "add lines before point"
  (declare (ignore c))
  (ce-reset-input)
  (ce-common-add (ce-mod inpoint (list-length buffer))))

(defun ce-command-delete (&optional c)
  "delete the region"
  (declare (ignore c))
  (ce-reset-input)
  (read-line)
  (let ((mlen (list-length buffer)))
   (ce-delete (ce-mod inpoint mlen) (ce-mod outpoint mlen))
   (setq outpoint inpoint)))

(defun ce-command-line-replace (&optional c)
  "replace the region"
  (declare (ignore c))
  (ce-reset-input)
  (let ((mlen (list-length buffer)))
   (let ((in (ce-mod inpoint mlen)) (out (ce-mod outpoint mlen)))
    (ce-delete in out)
    (ce-common-add in))))

; TODO: needs error handling
(defun ce-open (name)
  "function to open a file for editing"
  (setq filename name)
  (if (uiop:file-exists-p name)
   (handler-case (setq buffer (uiop:read-file-lines filename))
    (error (e) (setq err e) (format t "?%")))
   (format t "?%")))

(defun ce-command-open (&optional c)
  "open a file for editing"
  (declare (ignore c))
  (ce-reset-input)
  (let ((name (read-line)))
   (ce-open (if (string= "" name)
	     filename name))))

(defun ce-command-help (&optional c)
  "get help for commoned commands"
  (declare (ignore c))
  (ce-reset-input)
  (let ((key (read-char)))
   (if (char= #\Newline key)
    (format t "Welcome to commoned. try h<letter> to get help for a
specific command. the recognized commands are as follows:
狺" (remove-if-not 'not-num-new-p (mapcar 'car ce-commands-alist)))
    (progn (read-line) (help (cdr (assoc key ce-commands-alist))))))
  (format t "%"))

(defun ce-command-print (&optional c)
  "print a region"
  (declare (ignore c))
  (ce-reset-input)
  (read-line)
  (let ((mlen (list-length buffer)))
   (if (not (= 0 mlen))
    (let ((in (ce-mod inpoint mlen)) (out (1+ (ce-mod outpoint mlen))))
     (format t "狺" (subseq buffer in out)))
    (format t "?%"))))

(defun ce-command-reg-replace (&optional c)
  "do a sed-like replace"
  (declare (ignore c))
  (ce-reset-input)
  (let ((sep (read-char)))
   (if (char= #\Newline sep)
    (if (or (not query) (not ins) (not sfl))
     (format t "?%"))
    (let ((pat (ce-tokens (read-line) sep)))
     (setq query (car pat))
     (setq ins (nth 1 pat))
     (if (> (list-length pat) 2)
      (setq sfl (nth 2 pat))
      (setq sfl ""))))
   ()))

; TODO: needs error handling
(defun ce-command-write (&optional c)
  "write a file to disk"
  (declare (ignore c))
  (ce-reset-input)
  (let ((name (read-line)))
   (with-open-file (out (if (string= "" name) filename name)
			:direction :output
			:if-exists :overwrite
			:if-does-not-exist :create)
    (format out "狺" buffer))))

(defun ce-command-number (c)
  "input a number"
  (if (or (= 0 newpoint) (= 2 newpoint))
   (progn (setq newpoint (1+ newpoint)) (setq outpoint 0)))
  (setq outpoint (+ (* 10 outpoint) (digit-char-p c))))

